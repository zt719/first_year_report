\chapter{Appendix}

\section{Agda Code - Higher-Order Containers Examples}

\begin{code}[hide]
open import Level renaming (zero to lzero; suc to lsuc)
open import Data.Empty
open import Data.Unit
open import Data.Sum
open import Data.Product
open import Function.Base hiding (_$_)
open import Relation.Binary.PropositionalEquality hiding ([_])

open import outcomes
\end{code}

\subsubsection*{Morphisms}\label{Hom}

\begin{code}
idNfHom : NfHom t t
idNfHom {t = ne spr} = ne (id ◃ (λ x s → id) ◃ λ x s q → ε)
idNfHom {t = lam t} = lam (idNfHom {t = t})

_∘nfHom_ : NfHom u w → NfHom t u → NfHom t w
_∘spHom_ : SpHom us ws → SpHom ts us → SpHom ts ws

lam f ∘nfHom lam g = lam (f ∘nfHom g)
ne (f ◃ g ◃ h) ∘nfHom ne (f' ◃ g' ◃ h') = ne (
  (f ∘ f')
  ◃ (λ x s → g' x s ∘ g x (f' s))
  ◃ λ x s q → (h x (f' s) q) ∘spHom h' x s (g x (f' s) q)
  )

ε ∘spHom ε = ε
ε ∘spHom (g , gs) = g , gs
(f , fs) ∘spHom ε = f , fs
(f , fs) ∘spHom (g , gs) = (f ∘nfHom g) , (fs ∘spHom gs)

⊤nf : Nf Γ A
⊤nf {Γ} {*} = ne (⊤ ◃ (λ{ x tt → ⊥ }) ◃ λ{ x tt () })
⊤nf {Γ} {A ⇒ B} = lam ⊤nf

⊥nf : Nf Γ A
⊥nf {Γ} {*} = ne (⊥ ◃ (λ x ()) ◃ (λ x ()))
⊥nf {Γ} {A ⇒ B} = lam ⊥nf

_×nf_ : Nf Γ A → Nf Γ A → Nf Γ A
lam t ×nf lam u = lam (t ×nf u)
_×nf_ {Γ} {B} (ne (S ◃ P ◃ R)) (ne (T ◃ Q ◃ L)) = ne (S' ◃ P' ◃ R')
  where
  S' : Set
  S' = S × T

  P' : Var Γ A → S' → Set
  P' x (s , t) = P x s ⊎ Q x t

  R' : (x : Var Γ A) (s : S') → P' x s → Sp Γ A B
  R' x (s , t) (inj₁ p) = R x s p
  R' x (s , t) (inj₂ q) = L x t q

_⊎nf_ : Nf Γ A → Nf Γ A → Nf Γ A
lam t ⊎nf lam u = lam (t ⊎nf u)
_⊎nf_ {Γ} {B} (ne (S ◃ P ◃ R)) (ne (T ◃ Q ◃ L)) = ne (S' ◃ P' ◃ R')
  where
  S' : Set
  S' = S ⊎ T

  P' : Var Γ A → S' → Set
  P' x (inj₁ s) = P x s
  P' x (inj₂ t) = Q x t

  R' : (x : Var Γ A) (s : S') → P' x s → Sp Γ A B
  R' x (inj₁ s) p = R x s p
  R' x (inj₂ t) q = L x t q

!nf : (t : Nf Γ A) → NfHom t ⊤nf
!nf (lam t) = lam (!nf t)
!nf (ne (S ◃ P ◃ R)) = ne ((λ _ → tt) ◃ (λ x s ()) ◃ λ x s ())

¿nf : (t : Nf Γ A) → NfHom ⊥nf t
¿nf (lam t) = lam (¿nf t)
¿nf (ne (S ◃ P ◃ R)) = ne ((λ ()) ◃ (λ x ()) ◃ λ x ())

π₁nf : (t u : Nf Γ A) → NfHom (t ×nf u) t
π₁nf (lam t) (lam u) = lam (π₁nf t u)
π₁nf {Γ} {B} (ne (S ◃ P ◃ R)) (ne (T ◃ Q ◃ L)) = ne (f ◃ g ◃ h)
  where
  f : S × T → S
  f (s , t) = s

  g : (x : Var Γ A) (st : S × T) → P x (f st) → P x (st .proj₁) ⊎ Q x (st .proj₂)
  g x (s , t) p = inj₁ p

  h : (x : Var Γ A) (st : S × T) (q : P x (f st)) → SpHom (R x (f st) q) (R x (f st) q)
  h x (s , t) q = ε

i₁nf : (t u : Nf Γ A) → NfHom t (t ⊎nf u)
i₁nf (lam t) (lam u) = lam (i₁nf t u)
i₁nf {Γ} (ne (S ◃ P ◃ R)) (ne (T ◃ Q ◃ L)) = ne (f ◃ g ◃ h)
  where
  f : S → S ⊎ T
  f s = inj₁ s

  g : (x : Var Γ A) (s : S) → P x s → P x s
  g x s p = p

  h : (x : Var Γ A) (s : S) (q : P x s) → SpHom (R x s (g x s q)) (R x s q)
  h x s q = ε

<_,_>nf : NfHom t u → NfHom t w → NfHom t (u ×nf w)
< lam tu , lam tv >nf = lam < tu , tv >nf
<_,_>nf {Γ} {B} (ne (f₁ ◃ g₁ ◃ h₁)) (ne (f₂ ◃ g₂ ◃ h₂)) = ne (ff ◃ gg ◃ hh)
  where
  ff : _
  ff = < f₁ , f₂ >

  gg : (x : Var Γ A) (s : _) → _
  gg x s (inj₁ q₁) = g₁ x s q₁
  gg x s (inj₂ q₂) = g₂ x s q₂

  hh : (x : Var Γ A) (s : _) (q : _) → _
  hh x s (inj₁ q₁) = h₁ x s q₁
  hh x s (inj₂ q₂) = h₂ x s q₂

[_,_]nf : NfHom t w → NfHom u w → NfHom (t ⊎nf u) w
[ lam tv , lam uv ]nf = lam ([ tv , uv ]nf)
[_,_]nf {Γ} {B} (ne (f₁ ◃ g₁ ◃ h₁)) (ne (f₂ ◃ g₂ ◃ h₂)) = ne (ff ◃ gg ◃ hh)
  where
  ff : _
  ff = [ f₁ , f₂ ]

  gg : (x : Var Γ A) (s : _) → _
  gg x (inj₁ s₁) q₁ = g₁ x s₁ q₁
  gg x (inj₂ s₂) q₂ = g₂ x s₂ q₂

  hh : (x : Var Γ A) (s : _) (q : _) → _
  hh x (inj₁ s₁) q₁ = h₁ x s₁ q₁
  hh x (inj₂ s₂) q₂ = h₂ x s₂ q₂
\end{code}

\subsubsection*{SCwF}\label{SCwF}

\begin{code}
idNfs : Nfs Γ Γ
idNfs {∙} = ε
idNfs {Γ ▹ A} = idNfs ↑

_∘nfs_ : Nfs Δ Γ → Nfs Θ Δ → Nfs Θ Γ
ε ∘nfs γ = ε
(δ , t) ∘nfs γ = (δ ∘nfs γ) , (t [ γ ]nf)

π₁ : Nfs Δ (Γ ▹ A) → Nfs Δ Γ
π₁ (γ , t) = γ

π₂ : Nfs Δ (Γ ▹ A) → Nf Δ A
π₂ (γ , t) = t

wk : Nfs (Γ ▹ A) Γ
wk = π₁ idNfs

nvz : Nf (Γ ▹ A) A
nvz = π₂ idNfs

nvs : Nf Γ A → Nf (Γ ▹ B) A
nvs t = t [ wk ]nf

<_> : Nf Γ A → Nfs Γ (Γ ▹ A)
< t > = idNfs , t

π₁β : π₁ (γ , t) ≡ γ
π₁β = refl

π₂β : π₂ (γ , t) ≡ t
π₂β = refl

πη : (π₁ γ , π₂ γ) ≡ γ
πη {γ = γ , t} = refl

,∘ : (γ , t) ∘nfs δ ≡ (γ ∘nfs δ , t [ δ ]nf)
,∘ = refl
\end{code}

\subsubsection*{Example - $Htm$}\label{Htm}

\begin{code}
Htm : Tm ∙ ((* ⇒ *) ⇒ * ⇒ *)
Htm = lam (lam (Πtm (⊤ ⊎ ⊤) (λ
  { (inj₁ tt) → var vz
  ; (inj₂ tt) → var (vs vz) $ ((var (vs vz)) $ var vz)
  })))
\end{code}

\subsubsection*{Example - $Hnf$}\label{Hnf}

\begin{code}
Hnf : Nf ∙ ((* ⇒ *) ⇒ * ⇒ *)
Hnf = lam (lam (ne (S ◃ P ◃ R)))
  where
  Γ₀ : Con
  Γ₀ = (∙ ▹ * ⇒ * ▹ *)

  S : Type
  S = ⊤ ⊎ ⊤

  P : Var Γ₀ A → S → Type
  P vz (inj₁ tt) = ⊤
  P vz (inj₂ tt) = ⊥
  P (vs vz) (inj₁ tt) = ⊥
  P (vs vz) (inj₂ tt) = ⊤

  R : (x : Var Γ₀ A) (s : S) → P x s → Sp Γ₀ A *
  R vz (inj₁ tt) tt = ε
  R (vs vz) (inj₂ tt) tt = FX , ε
    where
    FX : Nf Γ₀ *
    FX = ne (FX-S ◃ FX-P ◃ FX-R)
      where
      FX-S : Type
      FX-S = ⊤

      FX-P : Var Γ₀ A → FX-S → Type
      FX-P vz tt = ⊥
      FX-P (vs vz) tt = ⊤

      FX-R : (x : Var Γ₀ A) (s : FX-S) → FX-P x s → Sp Γ₀ A *
      FX-R (vs vz) tt tt = X , ε
        where
        X : Nf Γ₀ *
        X = ne (X-S ◃ X-P ◃ X-R)
          where
          X-S : Type
          X-S = ⊤

          X-P : Var Γ₀ A → X-S → Type
          X-P vz tt = ⊤
          X-P (vs vz) tt = ⊥

          X-R : (x : Var Γ₀ A) (s : X-S) → X-P x s → Sp Γ₀ A *
          X-R vz tt tt = ε
          X-R (vs vz) tt ()
\end{code}