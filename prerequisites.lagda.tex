\begin{code}[hide]
open import Data.Empty
open import Data.Unit
open import Data.Nat

Type : Set₁
Type = Set

Type₁ : Set₂
Type₁ = Set₁

infixr 5 _∷_
infix  4 _≡_
\end{code}

\chapter{Prerequisites and Settings}

We assume the reader has basic knowledge in logics, functional programming and category theory. We introduce type theory and set it as our framework. In particular, we use Agda, an implemented programming language, for our study and in this report.

Assuming \AgdaBound{a} \AgdaSymbol{:} \AgdaBound{A} and \AgdaBound{b} \AgdaSymbol{:} \AgdaBound{B}, the following are standard Agda syntax for common algebraic data types:

\begin{itemize}
  \item{\AgdaFunction{⊥} - Empty Type, with no term}
  \item{\AgdaRecord{⊤} - Unit Type, with \AgdaInductiveConstructor{tt} \AgdaSymbol{:} \AgdaRecord{⊤}}
  \item{\AgdaRecord{×} - Product Type, with \AgdaBound{a} \AgdaInductiveConstructor{,} \AgdaBound{b} \AgdaSymbol{:} \AgdaBound{A} \AgdaRecord{×} \AgdaBound{B}}
  \item{\AgdaDatatype{⊎} - Coproduct (Sum) Type, with \AgdaInductiveConstructor{inj₁} \AgdaBound{a} \AgdaSymbol{:} \AgdaBound{A} \AgdaRecord{⊎} \AgdaBound{B}, and \AgdaInductiveConstructor{inj₂} \AgdaBound{b} \AgdaSymbol{:} \AgdaBound{A} \AgdaRecord{⊎} \AgdaBound{B}}
\end{itemize}

\section{Type Theory}

Type theory is a formal language of mathematical logics, designed to serve as a foundation for mathematics and programming languages. Various flavors of type theories exist, differing in their treatment of equality, interpretation of types, computational univalence, etc. We introduce basic concepts in type theory and outline the development.

\subsection*{Universes}

In type theory, everything has a type, even types have a type. We call it the universe and denote it as \AgdaPrimitive{Type}. Such that:

\[ \AgdaFunction{⊥ } \texttt{, } \AgdaRecord{⊤ } \texttt{, } \AgdaDatatype{ℕ } \texttt{, ... } \AgdaSymbol{: } \AgdaPrimitive{Type} \]

But what is the type of \AgdaPrimitive{Type}? It turns out if we naively postulate \AgdaPrimitive{Type} \AgdaSymbol{:} \AgdaPrimitive{Type}, then it leads to Girard’s paradox which causes inconsistency in the system. The solution is to build hierarchy of the universes:

\[ \AgdaPrimitive{Type } \AgdaSymbol{: } \AgdaPrimitive{Type₁ } \AgdaSymbol{: } \AgdaPrimitive{Type₂ } \AgdaSymbol{: } \texttt{...} \]

where each universe is bigger than the previous ones. In addition, if a type is in a universe, it can be lifted into a bigger universe.

\subsection*{Type Families}

The concepts of type families can be viewed as generalization of ordinary functions, where the codomain is allowed to be \AgdaPrimitive{Type}. For example a predicate that tells the evenness can be defined as a type family over \AgdaDatatype{ℕ}:

\begin{code}
isEven : ℕ → Type
isEven zero = ⊤
isEven (suc zero) = ⊥
isEven (suc (suc n)) = isEven n
\end{code}

\subsection*{MLTT}

Per Martin-L\"{o}f introduced MLTT as a constructive foundations for mathematics. It is also known as the intuitionistic type theory or dependent type theory. Form a programming point of view, it adds type level control of data types. For example we can define vector:

\begin{code}
data Vec (A : Type) : ℕ → Type where
  [] : Vec A 0
  _∷_ : {n : ℕ} → A → Vec A n → Vec A (suc n)
\end{code}

According to this definition, \AgdaDatatype{Vec} \AgdaDatatype{ℕ} \AgdaNumber{3} should have exactly 3 numbers:

\begin{code}
vec : Vec ℕ 3
vec = 1 ∷ 2 ∷ 3 ∷ []
\end{code}

Type families also generalize function type \AgdaFunction{→} into dependent function type \AgdaFunction{Π} and pair type \AgdaDatatype{×} into dependent pair type \AgdaDatatype{Σ}.

\begin{code}
Π : (A : Type) (B : A → Type) → Type
Π A B = (a : A) → B a

record Σ (A : Type) (B : A → Type) : Type where
  constructor _,_
  field
    proj₁ : A
    proj₂ : B proj₁
\end{code}

We can now extend the Curry-Howard correspondence by interpreting \AgdaFunction{Π} as universal quantification and \AgdaRecord{Σ} as existential quantification. For example, we can show there is some even number through \AgdaRecord{Σ}:

\begin{code}
∃Even : Σ ℕ isEven
∃Even = 2 , tt
\end{code}

\subsection*{Equalities}

It is also important to distinguish different notions of equality. The definitional equality is a very strong notion of equality which two terms are reduced to the same normal forms. However, most theorems and results of practical interests do not hold definitionally. Their proofs normally involves doing case analysis, building extra lemmas, etc. We refer this weaker notion of equality the propositional equality.

In Agda, (definitional) equality is represented by identity type, which is a binary type family over any type \AgdaBound{A}, and it is witnessed by \AgdaInductiveConstructor{refl}.

\begin{code}
data _≡_ {A : Type} (x : A) : A → Type where
  refl : x ≡ x
\end{code}

Therefore, if a proposition is hold directly by \AgdaInductiveConstructor{refl}, then It is definitional equality. In contrast, if an explicit proof is required, then it is propositional equality.

\begin{code}
thm0 : 1 + 1 ≡ 2
thm0 = refl

thm1 : (n : ℕ) → 1 + n ≡ n + 1
thm1 zero = refl
thm1 (suc n) = cong suc (thm1 n)
  where
  cong : {A B : Type} (f : A → B) {x y : A} → x ≡ y → f x ≡ f y
  cong f refl = refl
\end{code}

\subsection*{Function Extensionality}

Here arises another question - what is the good notion of equality of functions? Function extensionality suggests that two functions should considered as equal if they yield the same output for every input. However, in intensional type theory, for example Agda, where equality is defined as definitional equality, the \AgdaFunction{funExt} is not provable within the system and must instead be postulated.

\begin{code}
postulate
  funExt : {A : Type} {B : A → Type} {f g : (x : A) → B x} →
           ((x : A) → f x ≡ g x) → f ≡ g
\end{code}

On the other hand, an extensional type theory suggest to treat two notions of equality as the same one. That is, it forces the proposition equality back into the definitional one. However, this approach has significant drawbacks: type checking becomes undecidable, and computation loses canonicity.

\subsection*{UIP}

Given two terms \AgdaBound{x y} \AgdaSymbol{:} \AgdaBound{A} and two proofs of equality \AgdaBound{p q} \AgdaSymbol{:} \AgdaBound{x} \AgdaDatatype{≡} \AgdaBound{y}, should we have \AgdaBound{p} \AgdaDatatype{≡} \AgdaBound{q}?

The answer is, in MLTT, the identity type can have multiple district proofs, so \AgdaBound{p} \AgdaFunction{≢} \AgdaBound{q} may be possible. However, one could add the uniqueness of identity proofs, aka K, as an axiom to force \AgdaBound{p} \AgdaDatatype{≡} \AgdaBound{q} always holds. From another point of view, types are treated much like sets, as in set theory two terms being equal is just a proposition and nobody talks about the equality of propositions.

\subsection*{HoTT}

The homotopy type theory extends MLTT further and provides an alternative interpretation of types. By adopting the concepts from homotopy theory, it types are viewed as spaces and equalities are viewed as paths between spaces.

We will omit a discussion of the univalence axiom, as it falls outside the scope of this report.

\subsection*{H-Levels}

UIP does not hold in HoTT and it is not allowed to be postulated. Instead, HoTT introduces the concepts of h(omotopy)-levels in order to interpret and fix this nested equalities issue. The idea is to classify and assign each type (or space) to a h-level.

At the base, a (−2)-type, or the contractible, is one in which has exactly one term (or center point), up to equality (or homotopy):

\begin{code}
record isContr (A : Type) : Type where
  field
    center : A
    paths : (x : A) → center ≡ x
\end{code}

A (−1)-type, or the proposition, is one that all terms are equal:

\begin{code}
isProp : Type → Type
isProp A = (x y : A) → x ≡ y
\end{code}

A 0-type, or set, is one in which all equalities are equal.

\begin{code}
isSet : Type → Type
isSet A = (x y : A) (p q : x ≡ y) → p ≡ q
\end{code}

Indeed, one could inductively define \AgdaFunction{isHLevel} over \AgdaDatatype{ℕ} to get general form of higher homotopy type.

\begin{code}
isOfHLevel : ℕ → Type → Type
isOfHLevel zero A = isContr A
isOfHLevel (suc n) A = (x y : A) → isOfHLevel n (x ≡ y)
\end{code}

\subsection*{CTT}

One of the major drawbacks of HoTT is its lack of computability. Cubical type theory is then developed as a computable version of HoTT. In particular, equality (or path) of type \AgdaBound{A} is redefined as a function \AgdaGeneralizable{I} \AgdaSymbol{→} \AgdaBound{A}, where \AgdaGeneralizable{I} is a primitive type called interval. There are two terms in an interval - \AgdaInductiveConstructor{i0} \texttt{,} \AgdaInductiveConstructor{i1} \AgdaSymbol{:} \AgdaGeneralizable{I}, being used to parametrize two endpoints of a path. In Cubical Agda, path type is defined as follow.

\begin{code}[hide]
module _ {I : Type} where
\end{code}

\begin{code}
  Path : (A : Type) → A  → A → Type
  Path A a₀ a₁ = I → A
\end{code}

A path \AgdaFunction{p} comes also with two side constraints: \AgdaFunction{p }\AgdaInductiveConstructor{i0 }\AgdaSymbol{= }\AgdaBound{a₀} and \AgdaFunction{p }\AgdaInductiveConstructor{i1 }\AgdaSymbol{= }\AgdaBound{a₁}

We can explicitly define path of path to capture equalities of equalities:

\begin{code}
  Square : (A : Type) {a₀₀ a₀₁ a₁₀ a₁₁ : A} 
    → Path A a₀₀ a₀₁ → Path A a₁₀ a₁₁ 
    → Path A a₀₀ a₁₀ → Path A a₀₁ a₁₁
    → Type
  Square A top bottom left right = I → I → A
\end{code}

Again a square \AgdaFunction{s} should satisfy the cubical side conditions which makes the function evaluation to each \AgdaBound{a} point-wisely. Finally, one could repeat this to define cubes and general higher cubes.

\section{Non-Cubical Settings}

Terence Tao once describes three stages in mathematical learning and practice: pre-rigorous, rigorous, and post-rigorous. The idea of post-rigorous is that, when one already know how to do thins rigorously, then he can move fluently between intuition, informal reasoning, and formal rigor as needed.

In our context, we conduct theoretical research within HoTT, exploring concepts such as the interpretation of containers as endofunctors on h-level sets. This requires us to explicitly track h-level fields in Cubical Agda. We did formalizations in both MLTT and CTT and decided to continue and present our code using vanilla Agda.

We now explicitly state our working assumptions. H-level checking is hidden, therefore equalities between equalities within set-level structures are ignored. We also assume function extensionality and minimize the use of universe levels for simplicity.