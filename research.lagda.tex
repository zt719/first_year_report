\chapter{Conducted Research}

In this section, we introduce inductive types, coinductive types and the categorical semantics of them. Then we give the definition of containers and demonstrate their properties. Finally, we introduce some contributions to the container model, such as higher functoriality, higher containers and their properties.

\section{Inductive Types and Coinductive Types}

We now give a definition of inductive types. An inductive type \AgdaDatatype{T} is given by a finite number of data constructors, such that they should follow some constraints, namely the \textit{formation rule}, \textit{introduction rule}, \textit{elimination rule} and \textit{computation rule}. We look at natural number type in detail, followed by other examples.

\begin{code}[hide]
{-# OPTIONS --guardedness #-}

open import Data.Empty
open import Data.Unit
open import Data.Product

open import Level

Type : Set₁
Type = Set
\end{code}

\subsubsection*{Indexed Type}

\begin{code}
data Maybe (X : Type) : Type where
  nothing : Maybe X
  just    : X → Maybe X
\end{code}

\begin{code}
data List (X : Type) : Type where
  [] : List X
  _∷_ : X → List X → List X
\end{code}

\begin{code}
record ℕ∞ : Type where
  coinductive
  field
    pred∞ : Maybe ℕ∞
open ℕ∞
\end{code}

\begin{code}
record Stream (X : Type) : Type where
  coinductive
  field
    head : X
    tail : Stream X
open Stream
\end{code}

\section{Categorical Semantics of Types}

\subsection{Initial Algebra and Terminal Coalgebra}

\section{Containers}

\begin{code}

\end{code}

\section{Higher Functoriality}

\section{Higher Containers}

\subsubsection*{Syntax}

\begin{code}
infixr 20 _⇒_
data Ty : Type where
  * : Ty
  _⇒_ : Ty → Ty → Ty
\end{code}

\begin{code}[hide]
private variable A B C : Ty
\end{code}

\begin{code}
infixl 5 _▷_
data Con : Type where
  •   : Con
  _▷_ : Con → Ty → Con
\end{code}

\begin{code}[hide]
private variable Γ Δ : Con
\end{code}

\begin{code}
data Var : Con → Ty → Type where
  vz : Var (Γ ▷ A) A
  vs : Var Γ A → Var (Γ ▷ B) A
\end{code}

\begin{code}[hide]
private variable x y : Var Γ A
\end{code}

\begin{code}
data Nf : Con → Ty → Set₁

record Ne (Γ : Con) (B : Ty) : Set₁

data Sp : Con → Ty → Ty → Set₁

data Nf where
  lam : Nf (Γ ▷ A) B → Nf Γ (A ⇒ B)
  ne  : Ne Γ * → Nf Γ *
\end{code}

\begin{code}[hide]
private variable t u : Nf Γ A
\end{code}

\begin{code}
record Ne Γ B where
  inductive
  field
    S : Set
    P : Var Γ A → S → Set
    R : (x : Var Γ A) (s : S) → P x s → Sp Γ A B
\end{code}

\begin{code}[hide]
private variable m n : Ne Γ A
\end{code}

\begin{code}
data Sp where
  ε   : Sp Γ A A
  _,_ : Nf Γ A → Sp Γ B C → Sp Γ (A ⇒ B) C
\end{code}

\begin{code}[hide]
private variable ts us : Sp Γ A B
\end{code}

\begin{code}
HCont : Ty → Set₁
HCont A = Nf • A
\end{code}

\subsubsection*{Semantics}

\begin{code}
⟦_⟧T : Ty → Set₁
⟦ * ⟧T = Set
⟦ A ⇒ B ⟧T = ⟦ A ⟧T → ⟦ B ⟧T

⟦_⟧C : Con → Set₁
⟦ • ⟧C = Lift (suc zero) ⊤
⟦ Γ ▷ A ⟧C = ⟦ Γ ⟧C × ⟦ A ⟧T

⟦_⟧v : Var Γ A → ⟦ Γ ⟧C → ⟦ A ⟧T
⟦ vz ⟧v (γ , a) = a
⟦ vs x ⟧v (γ , a) = ⟦ x ⟧v γ

⟦_⟧nf : Nf Γ A → ⟦ Γ ⟧C → ⟦ A ⟧T

⟦_⟧ne : Ne Γ * → ⟦ Γ ⟧C → Set

⟦_⟧sp : Sp Γ A B → ⟦ Γ ⟧C → ⟦ A ⟧T → ⟦ B ⟧T

⟦ lam x ⟧nf γ a = ⟦ x ⟧nf (γ , a)
⟦ ne x ⟧nf γ = ⟦ x ⟧ne γ

⟦_⟧ne {Γ} record { S = S ; P = P ; R = R } γ =
  Σ[ s ∈ S ] ({A : Ty} (x : Var Γ A) (p : P x s) → ⟦ R x s p ⟧sp γ (⟦ x ⟧v γ))

⟦ ε ⟧sp γ a = a
⟦ n , ns ⟧sp γ f = ⟦ ns ⟧sp γ (f (⟦ n ⟧nf γ))

⟦_⟧ : HCont A → ⟦ A ⟧T
⟦ x ⟧ = ⟦ x ⟧nf (lift tt)
\end{code}

\subsubsection*{Normalization}

\begin{code}[hide]
_-_ : (Γ : Con) → Var Γ A → Con
• - ()
(Γ ▷ A) - vz = Γ
(Γ ▷ A) - (vs x) = (Γ - x) ▷ A

wkv : (x : Var Γ A) → Var (Γ - x) B → Var Γ B
wkv vz y = vs y
wkv (vs x) vz = vz
wkv (vs x) (vs y) = vs (wkv x y)

data EqVar : Var Γ A → Var Γ B → Set where
  same : EqVar x x
  diff : (x : Var Γ A) (y : Var (Γ - x) B) → EqVar x (wkv x y)

eq : (x : Var Γ A) (y : Var Γ B) → EqVar x y
eq vz vz = same
eq vz (vs y) = diff vz y
eq (vs x) vz = diff (vs x) vz
eq (vs x) (vs y) with eq x y
eq (vs x) (vs .x)          | same = same
eq (vs x) (vs .(wkv x y')) | diff .x y' = diff (vs x) (vs y')

wkNf : (x : Var Γ A) → Nf (Γ - x) B → Nf Γ B
wkNe : (x : Var Γ A) → Ne (Γ - x) B → Ne Γ B
wkSp : (x : Var Γ A) → Sp (Γ - x) B C → Sp Γ B C

wkNf x (lam t) = lam (wkNf (vs x) t)
wkNf x (ne e) = ne (wkNe x e)

wkNe {Γ} {A} {C} x record { S = S ; P = P ; R = R }
  = record { S = S ; P = P' ; R = R' }
  where
  P' : Var Γ B → S → Set
  P' y  s with eq x y
  P' .x s | same = ⊥
  P' y  s | diff .x y' = P y' s

  R' : (y : Var Γ B) (s : S) → P' y s → Sp Γ B C
  R' y  s p with eq x y
  R' .x s () | same
  R' y  s p  | diff .x y' = wkSp x (R y' s p)

wkSp x ε = ε
wkSp x (n , ns) = wkNf x n , wkSp x ns

appSp : Sp Γ A (B ⇒ C) → Nf Γ B → Sp Γ A C
appSp ε u = u , ε
appSp (n , ns) u = n , appSp ns u
\end{code}

\begin{code}
nvar : Var Γ A → Nf Γ A
\end{code}

\begin{code}[hide]
ne2nf : Ne Γ A → Nf Γ A

nvar {Γ} {B} x =
  ne2nf (record { S = S ; P = P ; R = R })
  where
  S : Set
  S = ⊤
  
  P : Var Γ A → S → Set
  P y  tt with eq x y
  P .x tt | same = ⊤
  P y  tt | diff .x y' = ⊥

  R : (y : Var Γ A) (s : S) → P y s → Sp Γ A B
  R y tt p with eq x y
  R .x tt p | same = ε
  R y tt () | diff .x y'

ne2nf {Γ} {*} x = ne x
ne2nf {Γ} {A ⇒ C} record { S = S ; P = P ; R = R } =
  lam (ne2nf (record { S = S ; P = P' ; R = R' }))
  where
  P' : Var (Γ ▷ A) B → S → Set
  P' vz s = ⊥
  P' (vs x) s = P x s

  R' : (x : Var (Γ ▷ A) B) (s : S) → P' x s → Sp (Γ ▷ A) B C
  R' vz s ()
  R' (vs x) s p = appSp (wkSp vz (R x s p)) (nvar vz)
\end{code}

\begin{code}
_[_:=_] : Nf Γ B → (x : Var Γ A) → Nf (Γ - x) A → Nf (Γ - x) B

_<_:=_> : Sp Γ B C → (x : Var Γ A) → Nf (Γ - x) A → Sp (Γ - x) B C

_◇_ : Nf Γ A → Sp Γ A B → Nf Γ B

napp : Nf Γ (A ⇒ B) → Nf Γ A → Nf Γ B
\end{code}

\begin{code}[hide]
lam t [ x := u ] = lam (t [ vs x := wkNf vz u ])
ne {Γ} record { S = S ; P = P ; R = R } [ x := u ] =
  ne (record { S = S ; P = P' ; R = R' })
  where
  P' : Var (Γ - x) A → S → Set
  P' y s = P (wkv x y) s

  R' : (y : Var (Γ - x) A) (s : S) → P' y s → Sp (Γ - x) A *
  R' y s p = R (wkv x y) s p < x := u >
  
ε < x := u > = ε
(t , ts) < x := u > = (t [ x := u ]) , (ts < x := u >)

t ◇ ε = t
t ◇ (u , us) = napp t u ◇ us

napp (lam t) u = t [ vz := u ]
\end{code}

\section{Questions}
