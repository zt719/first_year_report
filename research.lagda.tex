\begin{code}[hide]
{-# OPTIONS --guardedness #-}

open import Data.Empty
open import Data.Unit
open import Data.Sum
open import Data.Product
open import Function.Base
open import Relation.Binary.PropositionalEquality

Type : Set₁
Type = Set

Type₁ : Set₂
Type₁ = Set₁

variable X Y I : Type
\end{code}

\chapter{Conducted Research}

In this section, we will first review related literatures. Then we outline the topics studied, such as categorical semantics of inductive and coinductive types, containers, W and M types, etc. Finally, we illustrate the current challenge.

\section{Literature Review}
TODO

\section{Types and Categorical Semantics}

\subsection{Inductive Types}

We now give a definition of inductive types. An inductive type \AgdaDatatype{A} is specified by a finite set of its data constructors, and a function \AgdaFunction{f} \AgdaSymbol{:} \AgdaDatatype{A} \AgdaSymbol{→} \AgdaDatatype{B}, where \AgdaDatatype{B} is arbitrary type.

\subsubsection*{Natural Number}

We can define natural number \texttt{ℕ = \{0, 1, 2, 3, ...\}} as an inductive type. The constructors of natural number \AgdaDatatype{ℕ} follows exactly the Peano's 1st and 6th axioms - "0 is a natural number" and "For every natural number n, S(n) is a natural number".

\begin{code}
data ℕ : Type where
  zero : ℕ
  suc : ℕ → ℕ
\end{code}

\begin{code}[hide]
{-# BUILTIN NATURAL ℕ #-}
\end{code}

 We also need to spell out the induction principle, also known as the eliminator, of the type. In this case, we are doing mathematical induction on \AgdaDatatype{ℕ}:

\begin{code}
indℕ : (P : ℕ → Type)
  → P zero
  → ((n : ℕ) → P n → P (suc n))
  → (n : ℕ) → P n
indℕ P z s zero = z
indℕ P z s (suc n) = s n (indℕ P z s n)
\end{code}

In principle, \AgdaFunction{indℕ} should be explicitly called to define any function out of \AgdaDatatype{ℕ}, which can be a rather verbose process. Fortunately, one is able to instead use pattern-matching to define functions, as it internally implements induction while ensuring correctness.

\begin{code}
double : ℕ → ℕ
double = indℕ (λ _ → ℕ) zero (λ n dn → suc (suc dn))

double' : ℕ → ℕ
double' zero = zero
double' (suc n) = suc (suc (double n))
\end{code}

\subsubsection*{Inductive Types are Initial Algebras}

In the categorical semantics, inductive types correspond to initial algebras.

\subsubsection*{Natural Number is Initial Algebra of Maybe}

Natural number (together with its constructor) is the initial algebra of the \AgdaDatatype{⊤⊎\_} functor, which is also known as the \AgdaDatatype{Maybe}. To prove this, we first combine and rewrite \AgdaInductiveConstructor{zero} and \AgdaInductiveConstructor{suc} into equivalent form:

\begin{code}
[z,s] : ⊤ ⊎ ℕ → ℕ
[z,s] (inj₁ tt) = zero
[z,s] (inj₂ n) = suc n
\end{code}

To show the initiality, we show for arbitrary algebra, there exists a unique morphism form algebra \texttt{ℕ}. That is to undefine \AgdaFunction{fold}:

\begin{code}
fold : (⊤ ⊎ X → X) → ℕ → X
fold α zero = α (inj₁ tt)
fold α (suc n) = α (inj₂ (fold α n))
\end{code}

Then we construct the morphism mapping part of \AgdaDatatype{⊤ ⊎\_} and check the following diagram commutes:

\[
\begin{tikzcd}[row sep=huge, column sep=5.0cm]
\AgdaDatatype{⊤ ⊎ ℕ} \arrow[r, "\AgdaFunction{⊤⊎₁ } \AgdaSymbol{(}\AgdaFunction{fold }\AgdaBound{α}\AgdaSymbol{)}"] \arrow[d, "\AgdaFunction{[z,s]}"]
& \AgdaDatatype{⊤ ⊎ }\AgdaBound{X} \arrow[d, "\AgdaBound{α}"] \\
\AgdaDatatype{ℕ} \arrow[r, "\AgdaFunction{fold }\AgdaBound{α}"]
& \AgdaBound{X}
\end{tikzcd}
\]

\begin{code}
⊤⊎₁ : (X → Y) → ⊤ ⊎ X → ⊤ ⊎ Y
⊤⊎₁ f (inj₁ tt) = inj₁ tt
⊤⊎₁ f (inj₂ x) = inj₂ (f x)

commute : (β : ⊤ ⊎ X → X) (x : ⊤ ⊎ ℕ)
  → fold β ([z,s] x) ≡ β (⊤⊎₁ (fold β) x)
commute β (inj₁ tt) = refl
commute β (inj₂ n) = refl
\end{code}

\subsection{Coinductive Types}

One of the greatest power of category theory is that, whenever we define something, we always get an opposite version for free. Indeed, if we inverse all the morphisms in above diagram, then we will:
\begin{itemize}
  \item{talk about the category of coalgebras;}
  \item{obtain a definition of conatural number \AgdaRecord{ℕ∞};}
  \item{derive that \AgdaRecord{ℕ∞} is the terminal coalgebra of \AgdaDatatype{⊤⊎\_}}
\end{itemize}

The definition of coalgebras is trivial. We define conatural number as a coinductive type:

\begin{code}
record ℕ∞ : Type where
  coinductive
  field
    pred∞ : ⊤ ⊎ ℕ∞
open ℕ∞
\end{code}

We also define the inverse of \AgdaFunction{fold}, which is \AgdaFunction{unfold}:

\begin{code}
unfold : (X → ⊤ ⊎ X) → X → ℕ∞
pred∞ (unfold α⁻ x) with α⁻ x 
... | inj₁ tt = inj₁ tt
... | inj₂ x' = inj₂ (unfold α⁻ x')
\end{code}

That gives rise to the following commutative diagram:

\[
\begin{tikzcd}[row sep=huge, column sep=5.0cm]
\AgdaBound{X} \arrow[r, "\AgdaFunction{unfold }\AgdaBound{α⁻}"] \arrow[d, "\AgdaBound{α⁻}"]
& \AgdaDatatype{ℕ∞} \arrow[d, "\AgdaField{pred∞}"] \\
\AgdaDatatype{⊤ ⊎ }\AgdaBound{X} \arrow[r, "\AgdaFunction{⊤⊎₁ }\AgdaSymbol{(}\AgdaFunction{unfold }\AgdaBound{α⁻}\AgdaSymbol{)}"]
& \AgdaDatatype{⊤ ⊎ ℕ∞}
\end{tikzcd}
\]

\section{Containers}

\subsection{Strict Positivity}

Containers are categorical and type-theoretical abstraction to describe strictly positive datatypes. A strictly positive type is the type where all data constructors do not include itself on the left-side of a function arrow in the domain. One typical counterexample is \AgdaDatatype{Weird}:

\begin{code}
{-# NO_POSITIVITY_CHECK #-}
data Weird : Set where
  foo : (Weird → ⊥) → Weird

¬weird : Weird → ⊥
¬weird (foo x) = x (foo x)

bad : ⊥
bad = ¬weird (foo ¬weird)
\end{code}

\AgdaDatatype{Weird} is not strictly positive as in the domain of \AgdaInductiveConstructor{foo}, \AgdaDatatype{Weird} itself appears on the left-side of \AgdaSymbol{→}. Losing strict positivity can cause issues like non-normalizable, non-terminating, inconsistency, etc. In this case, we can construct an term in the empty type using \AgdaFunction{¬weird} which is inconsistent to the definition.

\subsection{Syntax and Semantics}

A container is given by a shape \AgdaBound{S} \AgdaSymbol{:} \AgdaFunction{Type} with a position \AgdaBound{P} \AgdaSymbol{:} \AgdaBound{S} \AgdaSymbol{→} \AgdaFunction{Type}:

\begin{code}
record Cont : Type₁ where
  constructor _◃_
  field
    S : Type
    P : S → Type
\end{code}

\begin{code}[hide]
variable SP TQ : Cont
\end{code}

A container should give rise to a endofunctor \AgdaFunction{Type} \AgdaSymbol{⇒} \AgdaFunction{Type}. Again we explicitly distinguish mapping objects part and mapping morphisms part of functors:

\begin{code}
record ⟦_⟧₀ (SP : Cont) (X : Type) : Type where
  constructor _,_
  open Cont SP
  field
    s : S
    k : P s → X

⟦_⟧₁ : (SP : Cont) → (X → Y) → ⟦ SP ⟧₀ X → ⟦ SP ⟧₀ Y
⟦ SP ⟧₁ f (s , k) = s , f ∘ k
\end{code}

\subsection{Categorical Structure}

Containers and their morphisms form a category. The morphisms are defined as follow:

\begin{code}
record ContHom (SP TQ : Cont) : Type where
  constructor _◃_
  open Cont SP
  open Cont TQ renaming (S to T; P to Q)
  field
    f : S → T
    g : (s : S) → Q (f s) → P s
\end{code}

where we can also show that it has identity and composition.

\begin{code}
⟦_⟧Hom : ContHom SP TQ → (X : Set) → ⟦ SP ⟧₀ X → ⟦ TQ ⟧₀ X
⟦ f ◃ g ⟧Hom X (s , k) = f s , k ∘ g s
\end{code}

\subsection{Semiring Structure}

Containers are also known as \textbf{polynomial functors} as they forms a semiring structure. Namely, we can define zero, one, addition and multiplication for containers:

\begin{code}
zero-C : Cont
zero-C = ⊥ ◃ λ ()

one-C : Cont
one-C = ⊤ ◃ λ{ tt → ⊥ }

_×C_ : Cont → Cont → Cont
(S ◃ P) ×C (T ◃ Q) = (S × T) ◃ λ (s , t) → P s ⊎ Q t

_⊎C_ : Cont → Cont → Cont
(S ◃ P) ⊎C (T ◃ Q) = (S ⊎ T) ◃ λ{ (inj₁ s) → P s ; (inj₂ t) → Q t }
\end{code}

such that semiring laws should hold. In fact, both multiplication and addition are commutative, associative and left- and right-annihilated by their units. Even better, we can define \AgdaFunction{Π} and \AgdaFunction{Σ} for containers which are the infinite generalization of \AgdaFunction{×} and \AgdaFunction{⊎}:

\begin{code}[hide]
module _ where
  open Cont
\end{code}

\begin{code}
  ΠC : (I → Cont) → Cont
  ΠC {I} C⃗ = ((i : I) → C⃗ i .S) ◃ λ f → Σ[ i ∈ I ] C⃗ i .P (f i)

  ΣC : (I → Cont) → Cont
  ΣC {I} C⃗ = (Σ[ i ∈ I ] C⃗ i .S) ◃ λ (i , s) → C⃗ i .P s
\end{code}

\subsection{W and M}

We now give the definition of general form of inductive types, which is the \AgdaDatatype{W} type:

\begin{code}
data W (SP : Cont) : Type where
  sup : ⟦ SP ⟧₀ (W SP) → W SP
\end{code}

From the definition, \AgdaDatatype{W} is an inductive type that specified by a container. In another word, any inductive type can be specified by a strictly positive functor! We can retrieve the definition of \AgdaDatatype{ℕ} through the \AgdaDatatype{⊤ ⊎} functor:

\begin{code}
⊤⊎Cont : Cont
⊤⊎Cont = S ◃ P
  where
  S : Type
  S = ⊤ ⊎ ⊤

  P : S → Type
  P (inj₁ tt) = ⊥
  P (inj₂ tt) = ⊤

ℕW : Type
ℕW = W ⊤⊎Cont
\end{code}

Dually, the general form of coinductive types - \AgdaRecord{M} type is just the terminal coalgebra of containers, and we can redefine \AgdaRecord{ℕ∞} using \AgdaRecord{M}:

\begin{code}
record M (SP : Cont) : Type where
  coinductive
  field
    inf : ⟦ SP ⟧₀ (M SP)

ℕ∞M : Type
ℕ∞M = M ⊤⊎Cont
\end{code}

Commutative diagrams:

\[
\begin{tikzcd}[row sep=huge, column sep=5.0cm]
\AgdaRecord{⟦ }\AgdaBound{SP }\AgdaRecord{⟧₀ }\AgdaSymbol{(}\AgdaDatatype{W }\AgdaBound{SP}\AgdaSymbol{)} \arrow[r, "\AgdaFunction{⟦ }\AgdaBound{SP }\AgdaFunction{⟧₁ }\AgdaSymbol{(}\AgdaFunction{fold }\AgdaBound{α}\AgdaSymbol{)}"] \arrow[d, "\AgdaInductiveConstructor{sup}"]
& \AgdaRecord{⟦ }\AgdaBound{SP }\AgdaRecord{⟧₀ }\AgdaBound{X} \arrow[d, "\AgdaBound{α}"] \\
\AgdaDatatype{W }\AgdaBound{SP} \arrow[r, "\AgdaFunction{fold }\AgdaBound{α}"]
& \AgdaBound{X}
\end{tikzcd}
\]

\[
\begin{tikzcd}[row sep=huge, column sep=5.0cm]
\AgdaBound{X} \arrow[r, "\AgdaFunction{unfold }\AgdaBound{α⁻}"] \arrow[d, "\AgdaBound{α⁻}"]
& \AgdaRecord{M } \AgdaBound{SP}\arrow[d, "\AgdaField{inf}"] \\
\AgdaRecord{⟦ }\AgdaBound{SP }\AgdaRecord{⟧₀ }\AgdaBound{X} \arrow[r, "\AgdaFunction{⟦ }\AgdaBound{SP }\AgdaFunction{⟧₁ }\AgdaSymbol{(}\AgdaFunction{unfold }\AgdaBound{α⁻}\AgdaSymbol{)}"]
& \AgdaRecord{⟦ }\AgdaBound{SP }\AgdaRecord{⟧₀ }\AgdaSymbol{(}\AgdaRecord{M }\AgdaBound{SP}\AgdaSymbol{)}
\end{tikzcd}
\]

\section{Questions}

\subsection{Bush}

\begin{code}
record Bush₀ (X : Type) : Type where
  coinductive
  field
    head : X
    tail : Bush₀ (Bush₀ X)
open Bush₀

{-# TERMINATING #-}
Bush₁ : (X → Y) → Bush₀ X → Bush₀ Y
head (Bush₁ f a) = f (head a)
tail (Bush₁ f a) = Bush₁ (Bush₁ f) (tail a)
\end{code}

We now look at the definition of a coinductive type \AgdaRecord{Bush}. Is \AgdaRecord{Bush} \AgdaBound{X} a terminal coalgebra of any endofunctor? It turns out that previews scheme is no longer applicable. Th solution is to lift the space from \AgdaDatatype{Type} to \AgdaDatatype{Type} \AgdaSymbol{→} \AgdaDatatype{Type} and observe whether \AgdaRecord{Bush} is the terminal coalgebra of a higher endofunctor.

\[
\begin{tikzcd}[row sep=huge, column sep=5.0cm]
\AgdaBound{F} \arrow[r, "\AgdaFunction{unfold }\AgdaBound{α⁻}"] \arrow[d, "\AgdaBound{α⁻}"]
& \AgdaRecord{Bush₀} \arrow[d, "\AgdaFunction{< }\AgdaField{head }\AgdaFunction{, }\AgdaField{tail }\AgdaFunction{>}"] \\
\AgdaFunction{H }\AgdaBound{F} \arrow[r, "\AgdaDatatype{H₁ }\AgdaSymbol{(}\AgdaFunction{unfold }\AgdaBound{α⁻}\AgdaSymbol{)}"]
& \AgdaFunction{H }\AgdaRecord{Bush₀}
\end{tikzcd}
\]